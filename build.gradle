plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.graalvm.buildtools.native' version '0.9.28'
}

group = 'com.github.vendigo'
version = '0.0.2-SNAPSHOT'

ext {
	dockerImageName = "europe-southwest1-docker.pkg.dev/music-friends-362414/music-friends-repo/music-friends-bot:${version}"
}

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-neo4j'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-cache'

	implementation 'org.telegram:telegrambots:6.8.0'
	implementation 'one.util:streamex:0.8.2'
	implementation 'com.google.guava:guava:32.1.3-jre'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.assertj:assertj-core'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'org.skyscreamer:jsonassert:1.5.1'
	testImplementation "org.testcontainers:junit-jupiter:1.19.3"
	testImplementation "org.testcontainers:neo4j:1.19.3"
}

configurations.configureEach {
	exclude group:"commons-logging", module: "commons-logging"
}

tasks.named('test') {
	useJUnitPlatform()
}

jar {
	enabled = false
}

bootBuildImage {
	imageName = dockerImageName
}

tasks.register("deployToGCP") {
	group = "deploy"
	doLast {
		exec {
			executable = "docker"
			args = ["push", dockerImageName]
		}

		exec {
			executable = "gcloud.cmd"
			args = [
					"run",
					"services",
					"update",
					"music-friends-bot",
					"--platform=managed",
					"--image=$dockerImageName",
					"--region=europe-southwest1",
					"--quiet",
					"--project=music-friends-362414"
			]
		}
	}
}